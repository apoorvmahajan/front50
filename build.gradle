/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'io.spinnaker.project' version "$spinnakerGradleVersion" apply false
  id "org.jetbrains.kotlin.jvm" version "$kotlinVersion" apply false
  id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion" apply false
}

subprojects { project ->
  group = "com.netflix.spinnaker.front50"
  apply plugin: 'io.spinnaker.project'

  if ([korkVersion, fiatVersion].find { it.endsWith("-SNAPSHOT") }) {
    repositories {
      mavenLocal()
    }
  }

  // For internal-to-salesforce artifacts (e.g. kork)
  repositories {
    // Without removing jcenter/bintray, gradle fails because jcenter is no more
    // (https://status.bintray.com/) and returns a 403, and somehow gradle gives up
    // instead of querying other repos, etc.
    //
    // Ideally, we'd fix this by removing the code that adds jcenter in the first place
    // (https://github.com/spinnaker/spinnaker-gradle-project/blob/e06b62c0958e06e4e2e300ac7268f67e65bb0959/spinnaker-project-plugin/src/main/groovy/com/ne
    // but because jcenter is gone, that project currently (8-jul-21) doesn't build, so
    // it's difficult to make changes.
    all { ArtifactRepository repo ->
      if (repo.name.toLowerCase().contains('jcenter')
          || repo.name.toLowerCase().contains('bintray')) {
        remove repo
      }
    }
    maven {
      url 'https://nexus-proxy-prd.soma.salesforce.com/nexus/content/groups/public/'
      credentials {
        username System.getenv('NEXUS_USERNAME')
        password System.getenv('NEXUS_PASSWORD')
      }
    }
  }

  if (name != "front50-bom") {
    apply plugin: 'java-library'
    apply plugin: 'groovy'

    tasks.compileGroovy.enabled = false

    test {
      testLogging {
        exceptionFormat = 'full'
      }
    }

    tasks.withType(JavaExec) {
      if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8180'
      }
    }

    dependencies {
      implementation(platform("com.salesforce.spinnaker.kork:kork-bom:$korkVersion"))
      annotationProcessor(platform("com.salesforce.spinnaker.kork:kork-bom:$korkVersion"))
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
      testAnnotationProcessor(platform("com.salesforce.spinnaker.kork:kork-bom:$korkVersion"))

      implementation("net.logstash.logback:logstash-logback-encoder")

      compileOnly("org.projectlombok:lombok")
      annotationProcessor("org.projectlombok:lombok")
      testAnnotationProcessor("org.projectlombok:lombok")

      testImplementation("org.codehaus.groovy:groovy-all")
      testImplementation("org.spockframework:spock-core")
      testImplementation("org.springframework.boot:spring-boot-starter-test")
      testImplementation("org.spockframework:spock-spring")
      testImplementation("org.springframework:spring-test")
      testImplementation("org.hamcrest:hamcrest-core")
      testRuntimeOnly("cglib:cglib-nodep")
      testRuntimeOnly("org.objenesis:objenesis")

      // To fix https://nvd.nist.gov/vuln/detail/CVE-2021-27568
      // Introduced by net.minidev:json-smart used by com.jayway.jsonpath:json-path under different packages of spring framework and springboot
      // TODO: Move to kork by upgrading the version of jayway jsonpath and json-smart, or bumping up the spring framework version
      constraints {
         implementation("com.jayway.jsonpath:json-path:2.6.0")
      }
    }
  }
}

defaultTasks ':front50-web:run'
